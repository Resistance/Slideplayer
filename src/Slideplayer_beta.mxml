<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:sp="ee.ut.slideplayer.*" layout="absolute" initialize="init();" creationComplete="onCreationComplete()">
  <mx:Script source="logging.as"/>
  <mx:Style>
    Image {
      brokenImageSkin: Embed("broken.png");
    }
    Application {
      background-color: #cacaca;
      background-image: Embed("bg.jpg");
      background-repeat: repeat;
    }
  </mx:Style>
	<mx:Script>
		<![CDATA[

    import mx.controls.Alert;
    import mx.logging.ILogger;
    import mx.rpc.events.ResultEvent;

    private var log:ILogger = Log.getLogger("SlidePlayer_beta");

    private var _slideTimings:Array = [];
    private var _videoSource:String;
    private var _slidesSource:String;
    private var _config:String = "";
    private var _lectureTitle:String = "";

    private function init():void {
      initializeLogging();

      log.debug("Initialized");
    }

    private function onCreationComplete():void {
      log.debug("Creation complete");

      // Add external callback
      if (ExternalInterface.available) {
        ExternalInterface.addCallback("changeLecture", changeLecture);
      }
    }

    public function changeLecture(config:String, lecture:String):void {

      if (config != _config || lecture != _lectureTitle) {
        Alert.show("Config: " + config + "\nLecture: " + lecture);

        _config = config;
        _lectureTitle = lecture;

        configLoader.url = _config;
        configLoader.send();
      }
    }

    private function parseLoadedConfig(event:ResultEvent):void {
      log.debug("GOT RESULT");
      var lectures:XMLList = XML(event.result).lecture;
      for each (var lecture:XML in lectures) {
        if (lecture.attribute("title") == _lectureTitle) {
          _videoSource = "";
          log.debug("Lecture: " + lecture.attribute("title")
            + ", video: " + lecture.attribute("video")
            + ", slides: " + lecture.attribute("slides"));
          _videoSource = lecture.attribute("video").toString();

          _slidesSource = "";
          _slidesSource = lecture.attribute("slides").toString();

          _slideTimings = [];
          for each (var slide:XML in lecture.slide) {
            log.debug("Adding slide: " + slide.attribute("number").toString() + " at time " + slide.attribute("time").toString());
            _slideTimings.push({
              time:int(slide.attribute("time")),
              number:int(slide.attribute("number"))}
              );
          }
        }
      }
      log.debug("Config loaded. Starting Slideplayer...");
      slidePlayer.videoSource = _videoSource;
      slidePlayer.slidesSource = _slidesSource;
      slidePlayer.slideTimings = _slideTimings;
    }

    ]]>
	</mx:Script>
  <mx:HTTPService id="configLoader" method="get" resultFormat="e4x" result="parseLoadedConfig(event)"/>
  <sp:SlidePlayer id="slidePlayer" width="100%" height="100%" x="0" y="0"/>
  <mx:Button id="button12" label="Lecture 12" click="changeLecture('slideplayer-config.xml', 'loeng12')" x="0" y="0"/>
  <mx:Button id="button13" label="Lecture 13" click="changeLecture('slideplayer-config.xml', 'loeng13')" x="90" y="0"/>
</mx:Application>
